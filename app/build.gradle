apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

static def getVersionName() {
    def name = "2023.9.17"
    return name
}

def getVersionCode() {// 获取版本号
    def versionFile = file('version.properties')// 读取第一步新建的文件
    if (versionFile.canRead()) {// 判断文件读取异常
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionFile))
        def versionCode = versionProps['VERSION_CODE'].toInteger()// 读取文件里面的版本号
        def runTasks = gradle.startParameter.taskNames
        if (':app:assembleRelease' in runTasks || ':app:packageRelease' in runTasks
                || ':app:assembleProfessionalRelease' in runTasks
                || ':app:assembleNormalRelease' in runTasks) {
            // 版本号自增之后再写入文件（此处是关键，版本号自增+1）
            versionProps['VERSION_CODE'] = (++versionCode).toString()
            versionProps.store(versionFile.newWriter(), null)
        }
        return versionCode // 返回自增之后的版本号
    }
//    else {
//        throw new GradleException("Could not find version.properties!")
//    }
}

def appName = "tvbox"
def versionCodeNew = getVersionCode()
def versionNameNew = getVersionName()
android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId 'com.github.tvbox.osc.taka'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionCodeNew
        versionName versionNameNew
        multiDexEnabled true

        //设置room的Schema的位置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        ndk {
            //abiFilters 'arm64-v8a'
            abiFilters "armeabi-v7a"
        }

    }


    packagingOptions {
        exclude 'META-INF/androidx.localbroadcastmanager_localbroadcastmanager.version'
        exclude 'META-INF/androidx.customview_customview.version'
        exclude 'META-INF/androidx.legacy_legacy-support-core-ui.version'
        exclude 'META-INF/androidx.legacy_legacy-support-core-utils.version'
        exclude 'META-INF/androidx.slidingpanelayout_slidingpanelayout.version'
        exclude 'META-INF/androidx.*.version'
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/beans.xml'
    }

    signingConfigs {

        release {
            storePassword "000624"
            keyAlias "bunny"
            keyPassword "426000"
            storeFile file('../tvbox.jks')
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {

        debug {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled false

        }
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled true   // 是否代码混淆
            multiDexEnabled true // 防止方法数量超过65536导致错误
            shrinkResources true // 删除无用资源
            zipAlignEnabled true

        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def type = variant.buildType.name
            def fileName = appName + "_V" + versionNameNew + "_C" + versionCodeNew +
                    "_" + type + ".apk"
            outputFileName = fileName
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildFeatures {
        dataBinding = true
    }
    namespace 'com.github.tvbox.osc'

}

dependencies {
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
    api fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // for WebDAV
    implementation('com.github.thegrizzlylabs:sardine-android:0.8') {
        exclude group: 'com.squareup.okhttp3'
    }
    implementation 'com.github.NanoHttpd.nanohttpd:nanohttpd:nanohttpd-project-2.3.1'
    implementation 'com.github.NanoHttpd.nanohttpd:nanohttpd:nanohttpd-websocket-2.3.1'
    implementation 'com.google.zxing:core:3.5.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation files('libs\\thunder.jar')

    annotationProcessor 'androidx.room:room-compiler:2.5.1'
    implementation 'androidx.room:room-runtime:2.5.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.squareup.okhttp3:okhttp:3.12.11'
    implementation 'com.squareup.okio:okio:3.3.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.49-androidx'
    implementation 'com.kingja.loadsir:loadsir:1.3.8'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'me.jessyan:autosize:1.2.1'
    implementation('com.thoughtworks.xstream:xstream:1.4.20') {
        exclude group: 'xmlpull', module: 'xmlpull'
    }
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'com.orhanobut:hawk:2.0.1'

    implementation 'androidx.annotation:annotation:1.6.0'
    implementation "com.google.android.exoplayer:exoplayer-core:" + rootProject.ext.exoVersion
    implementation "com.google.android.exoplayer:exoplayer-dash:" + rootProject.ext.exoVersion
    implementation "com.google.android.exoplayer:exoplayer-hls:" + rootProject.ext.exoVersion
    implementation "com.google.android.exoplayer:exoplayer-rtsp:" + rootProject.ext.exoVersion
    implementation "com.google.android.exoplayer:extension-rtmp:" + rootProject.ext.exoVersion
    //implementation 'com.google.android.exoplayer:extension-okhttp:' + rootProject.ext.exoVersion

    implementation "com.aliyun.sdk.android:AliyunPlayer:5.5.1.0-full"
    implementation 'com.alivc.conan:AlivcConan:0.9.5'
    implementation 'xyz.doikki.android.dkplayer:dkplayer-ui:3.3.7'
    implementation 'xyz.doikki.android.dkplayer:videocache:3.3.7'

    implementation project(":quickjs")
    implementation project(":pyramid")

    implementation('org.xwalk:xwalk_shared_library:23.53.589.4') {
        exclude group: 'com.android.support'
    }
    implementation 'com.lzy.net:okgo:3.0.4'
    implementation 'com.owen:tv-recyclerview:3.0.0'

    implementation 'com.github.getActivity:XXPermissions:18.0'
    implementation 'org.jsoup:jsoup:1.15.4'
    implementation 'com.github.hedzr:android-file-chooser:v1.2.0-final'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'com.github.seven332:jcifs:1.3.19'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    configurations {
        all {
            exclude group: 'xpp3', module: 'xpp3'
        }
    }
}
